buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.8'
}

apply plugin: 'net.minecraftforge.gradle.forge'

ext.configFile = file "build.txt"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.mod_version}"
group = "com.pengu.lostthaumaturgy"
archivesBaseName = "LostThaumaturgy-${config.minecraft_version}"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "${config.forge_version}"
    runDir = "run"
    mappings = "snapshot_20170707"
    makeObfSourceJar = false
	replace 'required-after:hammercore', "required-after:hammercore@[${config.hammercore_version},)"
    replace '@VERSION@', "${config.mod_version}"
}

repositories {
	maven {
		name = "APengu Maven"
		url = "https://raw.githubusercontent.com/APengu/PenguLibs/gh-pages"
	}
}

dependencies {
	deobfCompile "apengu:HammerCore:1.12-1.9.4.0:deobf"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
	rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
	manifest {
		attributes 'FMLAT': 'lt_at.cfg'
	}
}

task srcJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
	manifest {
		attributes 'FMLAT': 'lt_at.cfg'
	}
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
	manifest {
		attributes 'FMLAT': 'lt_at.cfg'
	}
}

artifacts {
    archives deobfJar
    archives srcJar
}

curseforge {
	apiKey = System.env.CURSEFORGE_API_KEY
	project {
		id = "${config.project_id}"
		changelogType = 'html'
		changelog = ""
		releaseType = "${config.release_type}"
		addGameVersion "1.12.1"
		mainArtifact(jar) {
		  displayName = "[${config.minecraft_version}] Lost Thaumaturgy - ${config.mod_version}"
		}
		addArtifact(deobfJar) {
          displayName = "[${config.minecraft_version}] Lost Thaumaturgy (Deobfuscated) - ${config.mod_version}"
        }
        addArtifact(srcJar) {
          displayName = "[${config.minecraft_version}] Lost Thaumaturgy (Sources) - ${config.mod_version}"
        }
		relations {
            requiredLibrary 'Hammer-Core'
        }
	}
}